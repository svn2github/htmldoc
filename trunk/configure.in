dnl
dnl "$Id: configure.in,v 1.2 1999/11/08 17:12:39 mike Exp $"
dnl
dnl   Configuration script for HTMLDOC, an HTML document processing program.
dnl
dnl   Copyright 1997-1998 by Michael Sweet, all rights reserved.
dnl
dnl   HTMLDOC is distributed under the terms of the GNU General Public License
dnl   which is described in the file "COPYING-2.0".
dnl

AC_INIT(htmldoc/htmldoc.h)
AC_CONFIG_HEADER(config.h)

dnl Clear the debugging options unless the user asks for them...
OPTIM=""
AC_SUBST(OPTIM)
CFLAGS="${CFLAGS:=}"
CXXFLAGS="${CXXFLAGS:=}"

AC_ARG_ENABLE(debug, [  --enable-debug          turn on debugging [default=no]],
[if eval "test x$enable_debug = xyes"; then
	OPTIM="-g "
fi])

dnl Checks for programs...
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar)
AC_PATH_PROG(CHMOD,chmod)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(MV,mv)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG(RM,rm)

dnl Check for libraries...
LDFLAGS="$CXXFLAGS"
AC_SUBST(LDFLAGS)

AC_PATH_X
if test "$x_libraries" = "/usr/lib"; then
	echo "Ignoring X library directory \"$x_libraries\" requested by configure."
	x_libraries="NONE"
fi
if test ! "$x_libraries" = "NONE" -a ! "$x_libraries" = ""; then
	LDFLAGS="$LDFLAGS -L$x_libraries"
	if test "$uname" = "SunOS"; then
		LDFLAGS="$LDFLAGS -R$x_libraries"
	fi
fi
if test "$x_includes" = "/usr/include"; then
	echo "Ignoring X include directory \"$x_includes\" requested by configure."
	x_includes="NONE"
fi
if test ! "$x_includes" = "NONE" -a ! "$x_includes" = ""; then
	CFLAGS="$CFLAGS -I$x_includes"
	CXXFLAGS="$CXXFLAGS -I$x_includes"
fi
AC_CHECK_LIB(fltk,numericsort)

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for string functions.
AC_CHECK_FUNCS(strdup)
AC_CHECK_FUNCS(strcasecmp)
AC_CHECK_FUNCS(strncasecmp)

dnl Generate the Makedefs file...
AC_OUTPUT(Makedefs)

dnl
dnl End of "$Id: configure.in,v 1.2 1999/11/08 17:12:39 mike Exp $".
dnl
