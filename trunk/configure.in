dnl
dnl "$Id: configure.in,v 1.9 1999/11/23 13:38:49 mike Exp $"
dnl
dnl   Configuration script for HTMLDOC, an HTML document processing program.
dnl
dnl   Copyright 1997-1998 by Michael Sweet, all rights reserved.
dnl
dnl   HTMLDOC is distributed under the terms of the GNU General Public License
dnl   which is described in the file "COPYING-2.0".
dnl

AC_INIT(htmldoc/htmldoc.h)
AC_CONFIG_HEADER(config.h)
AC_PREFIX_DEFAULT(/usr)

dnl Clear the debugging options unless the user asks for them...
OPTIM=""
AC_SUBST(OPTIM)
CFLAGS="${CFLAGS:=}"
CXXFLAGS="${CXXFLAGS:=}"

AC_ARG_ENABLE(debug, [  --enable-debug          turn on debugging [default=no]],
[if eval "test x$enable_debug = xyes"; then
	OPTIM="-g "
fi])

dnl Checks for programs...
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar)
AC_PATH_PROG(CHMOD,chmod)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(MV,mv)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG(RM,rm)

dnl Figure out the correct "ar" command flags...
if test "$ac_cv_prog_ranlib" = ":"; then
	ARFLAGS="crs"
else
	ARFLAGS="cr"
fi

AC_SUBST(ARFLAGS)

dnl Check for libraries...
LDFLAGS="${LDFLAGS:=}"
AC_SUBST(LDFLAGS)

AC_PATH_X
if test "$x_libraries" = "/usr/lib"; then
	echo "Ignoring X library directory \"$x_libraries\" requested by configure."
	x_libraries="NONE"
fi
if test ! "$x_libraries" = "NONE" -a ! "$x_libraries" = ""; then
	LDFLAGS="$LDFLAGS -L$x_libraries"
	if test "`uname`" = "SunOS"; then
		LDFLAGS="$LDFLAGS -R$x_libraries"
	fi
fi
if test "$x_includes" = "/usr/include"; then
	echo "Ignoring X include directory \"$x_includes\" requested by configure."
	x_includes="NONE"
fi
if test ! "$x_includes" = "NONE" -a ! "$x_includes" = ""; then
	CFLAGS="$CFLAGS -I$x_includes"
	CXXFLAGS="$CXXFLAGS -I$x_includes"
fi
AC_CHECK_LIB(fltk,numericsort)

if test "$ac_cv_lib_fltk_numericsort" = yes -a "$ac_cv_prog_CXX" != ""; then
	LIBS="../gui/libgui.a $LIBS -lXext -lX11 -lm"
	GUI="gui"
else
	AC_MSG_WARN(C++ compiler or FLTK not available so no GUI will be built.)
	GUI=""
fi

AC_SUBST(GUI)

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for string functions.
AC_CHECK_FUNCS(strdup)
AC_CHECK_FUNCS(strcasecmp)
AC_CHECK_FUNCS(strncasecmp)

dnl Directories for config.h...
if test "$prefix" = "NONE"; then
	prefix=/usr
fi

AC_DEFINE_UNQUOTED(DOCUMENTATION, "$prefix/share/doc/htmldoc")
AC_DEFINE_UNQUOTED(HTML_DATA, "$prefix/share/htmldoc")

dnl Update compiler options...
if test -n "$GXX"; then
	if test -z "$OPTIM"; then
		if test "`uname`" = "HP-UX"; then
			# ZLIB has trouble with GCC 2.95 and -O2...
			OPTIM="-O1"
		else
	        	OPTIM="-O2"
		fi
	fi
else
	case `uname` in
        	IRIX*)
			if test -z "$OPTIM"; then
				if test `uname -r | sed -e '1,$s/\.//g'` -ge 62; then
					OPTIM="-O3 -OPT:Olimit=4000 -IPA"
        			else
					OPTIM="-O2"
				fi
			fi
			;;
		HP-UX*)
			if test -z "$OPTIM"; then
				OPTIM="+O2"
			fi
			;;
		SunOS*)
			# Solaris
			if test -z "$OPTIM"; then
				OPTIM="-O"
			fi
			;;
	esac
fi

dnl Generate the top-level Makefile and Makedefs file...
AC_OUTPUT(Makedefs Makefile)

dnl
dnl End of "$Id: configure.in,v 1.9 1999/11/23 13:38:49 mike Exp $".
dnl
