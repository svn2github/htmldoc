dnl
dnl "$Id: configure.in,v 1.23.2.1 2000/11/30 18:33:44 mike Exp $"
dnl
dnl   Configuration script for HTMLDOC, an HTML document processing program.
dnl
dnl   Copyright 1997-2000 by Michael Sweet, all rights reserved.
dnl
dnl   HTMLDOC is distributed under the terms of the GNU General Public License
dnl   which is described in the file "COPYING-2.0".
dnl

AC_INIT(htmldoc/htmldoc.h)
AC_CONFIG_HEADER(config.h)
AC_PREFIX_DEFAULT(/usr)

dnl Clear the debugging options unless the user asks for them...
OPTIM=""
AC_SUBST(OPTIM)
CFLAGS="${CFLAGS:=}"
CXXFLAGS="${CXXFLAGS:=}"

AC_ARG_ENABLE(debug, [  --enable-debug          turn on debugging [default=no]],
[if eval "test x$enable_debug = xyes"; then
	OPTIM="-g "
fi])

dnl Checks for programs...
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar)
AC_PATH_PROG(CHMOD,chmod)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(MV,mv)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG(RM,rm)

dnl Figure out the correct "ar" command flags...
if test "$ac_cv_prog_ranlib" = ":"; then
	ARFLAGS="crs"
else
	ARFLAGS="cr"
fi

AC_SUBST(ARFLAGS)

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for string functions.
AC_CHECK_FUNCS(strdup)
AC_CHECK_FUNCS(strcasecmp)
AC_CHECK_FUNCS(strncasecmp)

dnl Check for libraries...
LDFLAGS="${LDFLAGS:=}"
AC_SUBST(LDFLAGS)

AC_CHECK_LIB(m,pow)

OLDLIBS="$LIBS"

AC_PATH_X
if test "$x_libraries" = "/usr/lib"; then
	echo "Ignoring X library directory \"$x_libraries\" requested by configure."
	x_libraries="NONE"
fi
if test ! "$x_libraries" = "NONE" -a ! "$x_libraries" = ""; then
	LDFLAGS="$LDFLAGS -L$x_libraries"
	if test "`uname`" = "SunOS"; then
		LDFLAGS="$LDFLAGS -R$x_libraries"
	fi
fi
if test "$x_includes" = "/usr/include"; then
	echo "Ignoring X include directory \"$x_includes\" requested by configure."
	x_includes="NONE"
fi
if test ! "$x_includes" = "NONE" -a ! "$x_includes" = ""; then
	CFLAGS="$CFLAGS -I$x_includes"
	CXXFLAGS="$CXXFLAGS -I$x_includes"
fi

AC_CHECK_LIB(X11,XOpenDisplay)
AC_CHECK_LIB(Xext,XdbeQueryExtension)

AC_CHECK_LIB(fltk,numericsort)
if test "$ac_cv_lib_fltk_numericsort" = no; then
	ac_cv_lib_fltk_numericsort=""
	AC_CHECK_LIB(GL,glEnable)
	AC_CHECK_LIB(fltk,numericsort)
fi

AC_CHECK_LIB(Xpm,XpmCreatePixmapFromData)

NEWLIBS=""

AC_CHECK_LIB(jpeg,jpeg_CreateCompress,
	JPEGINC=""
	JPEG=""
	LIBS="-ljpeg $LIBS",
	JPEGINC="-I../jpeg"
	JPEG="jpeg"
	NEWLIBS="../jpeg/libjpeg.a $NEWLIBS")
AC_CHECK_LIB(z,gzgets,
	ZLIBINC=""
	ZLIB=""
	LIBS="-lz $LIBS",
	ZLIBINC="-I../zlib"
	ZLIB="zlib"
	NEWLIBS="../zlib/libz.a $NEWLIBS")
AC_CHECK_LIB(png,png_set_tRNS_to_alpha,
	PNGINC=""
	PNG=""
	LIBS="-lpng $LIBS",
	PNGINC="-I../png"
	PNG="png"
	NEWLIBS="../png/libpng.a $NEWLIBS")

LIBS="$NEWLIBS $LIBS"

if test "$ac_cv_lib_fltk_numericsort" = yes; then
	LIBS="../gui/libgui.a $LIBS"
	GUI="gui"
else
	AC_MSG_WARN(FLTK not available so no GUI will be built.)
	LIBS="$OLDLIBS"
	GUI=""
fi

AC_SUBST(GUI)
AC_SUBST(JPEG)
AC_SUBST(JPEGINC)
AC_SUBST(PNG)
AC_SUBST(PNGINC)
AC_SUBST(ZLIB)
AC_SUBST(ZLIBINC)

AC_DEFINE(HAVE_LIBJPEG)
AC_DEFINE(HAVE_LIBPNG)
AC_DEFINE(HAVE_LIBZ)

dnl Directories for config.h...
if test "$prefix" = "NONE"; then
	prefix=/usr
fi

AC_DEFINE_UNQUOTED(DOCUMENTATION, "$prefix/share/doc/htmldoc")
AC_DEFINE_UNQUOTED(HTML_DATA, "$prefix/share/htmldoc")

dnl Update compiler options...
if test -n "$GXX"; then
	# Since we don't use libstdc++, use gcc to avoid getting it linked in...
	CXX="gcc"

	# Set optimization flags...
	if test -z "$OPTIM"; then
		if test "`uname`" = "HP-UX"; then
			# ZLIB has trouble with GCC 2.95 and -O2...
			OPTIM="-O1"
		else
	        	OPTIM="-O2 -fforce-mem -fforce-addr -fcaller-saves"
		fi
	fi

	OPTIM="-fno-rtti $OPTIM"

	# See if GCC supports -fno-exceptions...
	echo "Testing if GCC supports -fno-exceptions..."
	OLDCFLAGS="$CFLAGS"
	CFLAGS="$CFLAGS -fno-exceptions"
	AC_TRY_COMPILE(,,
		OPTIM="$OPTIM -fno-exceptions"
		echo "Congratulations - your GCC supports exceptions; disabling them...",
		echo "GCC does not support exceptions...")
	CFLAGS="$OLDCFLAGS"

	# See if we are running Solaris; if so, try the -fpermissive option...
	if test "`uname`" = SunOS; then
		echo "Testing if GCC supports -fpermissive..."

		OLDCFLAGS="$CFLAGS"
		CFLAGS="$CFLAGS -fpermissive"
		AC_TRY_COMPILE(,,
			OPTIM="$OPTIM -fpermissive"
			echo "Enabling -fpermissive to work around X header bugs...",
			echo "The -fpermissive option is not required by your GCC...")
		CFLAGS="$OLDCFLAGS"
	fi
else
	case `uname` in
        	IRIX*)
			if test -z "$OPTIM"; then
				if test `uname -r | sed -e '1,$s/\.//g'` -ge 62; then
					OPTIM="-O3 -OPT:Olimit=5000"
        			else
					OPTIM="-O2"
				fi
			fi
			;;
		HP-UX*)
			if test -z "$OPTIM"; then
				OPTIM="+O2"
			fi
			;;
		SunOS*)
			# Solaris
			if test -z "$OPTIM"; then
				OPTIM="-xO4"
			fi
			;;
	esac
fi

dnl Generate the top-level Makefile and Makedefs file...
AC_OUTPUT(Makedefs Makefile)

dnl
dnl End of "$Id: configure.in,v 1.23.2.1 2000/11/30 18:33:44 mike Exp $".
dnl
